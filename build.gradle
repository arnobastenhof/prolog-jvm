subprojects {
    apply plugin: 'java'

    ext {
        antlrVersion = '4.5'
    }
    
    repositories {
        mavenCentral()
    }

    sourceCompatibility = 1.7
    version = '0.1.0-SNAPSHOT'
}

project(':compiler') {

    ext {
        antlrTargetDir = 'build/antlr'
        antlrPckgDir = 'com/prolog/jvm/parser'
    }

    configurations {
        antlr
    }
    
    dependencies {
        compile "org.antlr:antlr4-runtime:$antlrVersion"
        antlr   "org.antlr:antlr4:$antlrVersion"
    }

    task generateGrammarSource << {
        ext {
            srcDir = 'src/main/antlr'
            grammar = 'PrologJvmGrammar.g4'
        }

        def pckg = antlrPckgDir.replace('/','.')

        javaexec {
            main = 'org.antlr.v4.Tool'
            classpath configurations.antlr
            args '-o', "${antlrTargetDir}/${antlrPckgDir}", '-package', pckg, file("${srcDir}/${grammar}")
        }

    }

    task generateTestGrammarSource << {
        println 'TODO Arno'
    }

    compileJava {
        dependsOn 'generateGrammarSource'
        source    antlrTargetDir
    }
    compileTestJava.dependsOn 'generateTestGrammarSource'

}

project(':examples') {
    dependencies {
        compile project(':compiler')
    }
}
